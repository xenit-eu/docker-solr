plugins {
    id "eu.xenit.docker" version "5.5.1" apply false
    id "be.vbgn.ci-detect" version "0.5.0"
}

import java.util.stream.Collectors

def calcTags(version) {
    def tags = [
            "${version.major}.${version.minor}.${version.rev}".toString(),
            "${version.major}.${version.minor}".toString()
    ]

    if (version.maint) {
        tags += "${version.major}.${version.minor}.${version.rev}.${version.maint}".toString()
    }
    if (version.label) {
        tags += "${version.major}.${version.minor}.${version.rev}.${version.label}".toString()
        if (version.maint) {
            tags += "${version.major}.${version.minor}.${version.rev}.${version.maint}.${version.label}".toString()
        }
    }
    // For non-master/non-release builds, change the tags to contain branch and build number
    def isTestBuild = ci.isCi() && ci.branch != "master" && ci.branch != "release"
    if (isTestBuild) {
        tags = tags.stream().map({ it + "-build-${ci.buildNumber}" }).collect(Collectors.toList());
    }

    return tags
}

def calcRepository(flavor, enterprise, customized) {
    def repoName = (enterprise ? "docker.xenit.eu/alfresco-enterprise" : "docker.io/xenit")
    if (customized)
        return "${repoName}/alfresco-${flavor}-xenit"
    else
        return "${repoName}/alfresco-${flavor}"
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()

        maven {
            url 'https://artifacts.alfresco.com/nexus/content/repositories/public/'
            metadataSources {
                artifact()
            }
            content {
                includeModule 'org.alfresco', 'alfresco-solr'
                includeModule 'org.alfresco', 'alfresco-solr4-distribution'
                includeModule 'org.alfresco', 'alfresco-search-services'
            }
        }

        maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/private'
            credentials {
                username project.property('org.alfresco.maven.nexus.username')
                password project.property('org.alfresco.maven.nexus.password')
            }
        }

        maven {
            url "https://dl.cloudsmith.io/basic/xenit/private/maven/"
            credentials {
                username property("eu.xenit.cloudsmith.username")
                password property("eu.xenit.cloudsmith.password")
            }
        }
    }

    pluginManager.withPlugin('eu.xenit.docker-config') {
        docker {
            if (System.getenv("DOCKER_USER") != null) {
                registryCredentials {
                    username = System.getenv("DOCKER_USER")
                    password = System.getenv("DOCKER_PASSWORD")
                }
            } else {
                logger.debug "using default credentials"
            }
        }
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDirs = ["${rootProject.projectDir}/src/integrationTest/java"]
            }
        }
    }

    configurations {
        runtime
        integrationTestImplementation.extendsFrom testImplementation
        integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
        backupJar
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }
    dependencies {
        backupJar project(path: ":solr-backup")

        integrationTestImplementation "io.rest-assured:rest-assured:${restAssuredVersion}"
        integrationTestImplementation "io.rest-assured:json-path:${restAssuredVersion}"
        integrationTestImplementation "io.rest-assured:rest-assured-common:${restAssuredVersion}"
        integrationTestImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
        integrationTestImplementation "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
        integrationTestImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
        integrationTestImplementation "org.awaitility:awaitility:${awaitablityVersion}"

        integrationTestImplementation platform("com.amazonaws:aws-java-sdk-bom:${amazonVersion}")

        integrationTestImplementation('com.amazonaws:aws-java-sdk-core')
        integrationTestImplementation('com.amazonaws:aws-java-sdk-s3')
        integrationTestImplementation("com.amazonaws:aws-java-sdk-sts")
        integrationTestRuntimeOnly "org.glassfish.jaxb:jaxb-runtime:${jaxBVersion}"
    }
}
