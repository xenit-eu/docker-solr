import com.avast.gradle.dockercompose.tasks.ComposeDown
import com.bmuschko.gradle.docker.tasks.image.Dockerfile.From

subprojects {
    apply plugin: 'eu.xenit.docker'
    apply plugin: 'eu.xenit.docker-compose'

    configurations {
        telemetry
        loggings
        actuators
        backup
    }

    ext {
        alfredTelemetryVersion = '0.10.1'
        actuatorsVersion = '0.0.7'
        composeDir = "${rootProject.projectDir}/src/integrationTest/resources";
        // atm test without ssl enabled
        tests_use_ssl = false
        composeFiles = ["$composeDir/docker-compose-alfresco-non-ssl.yml",
                        "$composeDir/docker-compose-solr-non-ssl.yml",
                        "$composeDir/docker-compose-localstack.yml",
                        "$composeDir/docker-compose-db.yml"]
        solrSourceSetOverride = null
    }

    configurations {
        backupJar
    }

    dependencies {
        backupJar project(path: ":solr-backup")

        loggings("eu.xenit.logging:json-logging:0.0.11")
        actuators("eu.xenit.solr-actuators:solr-actuators:${actuatorsVersion}")
        implementation("javax.xml.bind:jaxb-api:2.3.1")
        implementation project(':solr-backup')
    }

    if (new File("${project.projectDir}/overload.gradle").exists())
        apply from: "${project.projectDir}/overload.gradle"


    def upstreamProjectName = ":upstream:${project.name}"
    def upstreamProject = project(upstreamProjectName)
    evaluationDependsOn(upstreamProjectName)
    def upstreamImage = upstreamProject.getTasks().getByName('buildDockerImage')
    buildDockerImage.dependsOn(upstreamImage)
    createDockerFile.dependsOn(upstreamImage)

    buildDockerImage {
        pull = false
    }

    dockerFile {
        dockerBuild {
            def enterprise = upstreamProject.alfrescoimage.contains("enterprise")
            repository = [calcRepository(upstreamProject.solr.flavor, enterprise, true)].get(0)
            tags = calcTags(upstreamProject.solr.version)
        }
    }


    createDockerFile {
        from(upstreamImage.getImageId().map({ new From(it) }))

        dependsOn(configurations.backupJar)

        // micrometer handler init script
        smartCopy("${project.projectDir}/../local/94-init-solr-logging.sh", "/docker-entrypoint.d/")
        smartCopy("${project.projectDir}/../local/95-init-solr-micrometer-metrics.sh", "/docker-entrypoint.d/")
        // jmx needs to be enabled
        environmentVariable 'JMX_ENABLED', 'true'
        // telemetry
        environmentVariable 'METRICS_JETTY_ENABLED', 'true'
        smartCopy "${project.projectDir}/../local/solrconfig_insight.xml", "/opt/alfresco-search-services/solrhome/templates/rerank/conf/solrconfig_insight.xml"
        smartCopy "${project.projectDir}/../local/jetty.xml", "/opt/alfresco-search-services/solr/server/etc/jetty.xml"
        smartCopy "${project.projectDir}/../local/solr-jetty-context.xml", "/opt/alfresco-search-services/solr/server/contexts/solr-jetty-context.xml"
        smartCopy files(configurations.telemetry.files), "/opt/alfresco-search-services/solrhome/lib/"
        label([
                "eu.xenit.prometheus.metrics-path": "/solr/alfresco/metrics",
                "eu.xenit.prometheus.params"      : "wt: ['dummy']"
        ])
        // loggings
        smartCopy files(configurations.loggings.files), "/opt/alfresco-search-services/solr/server/lib/ext/"
        // actuators
        smartCopy files(configurations.actuators.files), "/opt/alfresco-search-services/solrhome/lib/"
        // backup
        smartCopy files(configurations.backup.files), "/opt/alfresco-search-services/solrhome/lib/"
        smartCopy("${project.projectDir}/../local/93-restore-from-backup.sh", "/docker-entrypoint.d/")
        smartCopy("${project.projectDir}/../local/solr.xml", "/opt/alfresco-search-services/solrhome/solr.xml")
        smartCopy configurations.backupJar, "/opt/alfresco-search-services/solrhome/lib/"

        if (project.solrSourceSetOverride != null) {
            doFirst {
                println("Note: the source code of the alfresco-search-*.jar will be overridden with code from (${project.solrSourceSetOverride}).")
            }

            // 1. Copy the classes from the sourceSet to a temp directory.
            project.solrSourceSetOverride.classesDirs.each { classesDir ->
                smartCopy(classesDir, "/tmp/alfresco-search-jar-source-override/")
            }

            // 2. Copy the classes from the temp directory to the alfresco-search-*.jar.
            // Find the JAR.
            runCommand('alfresco_search_jar_location=$(ls /opt/alfresco-search-services/solr/server/solr-webapp/webapp/WEB-INF/lib/alfresco-search*.jar) && ' +
                        // Fail if the JAR is not found.
                        'if [ -z "$alfresco_search_jar_location" ]; then echo "The alfresco-search-*.jar was not found."; exit 1; else echo "Found alfresco-search-*.jar at ($alfresco_search_jar_location)."; fi && ' +
                        // Copy the classes from the temp directory to the JAR.
                        'cd /tmp/alfresco-search-jar-source-override && ' +
                        'for file in $(find /tmp/alfresco-search-jar-source-override -name "*.class"); do ' +
                            'echo "Copying ($file) to ($alfresco_search_jar_location)."; ' +
                            'jar uf "$alfresco_search_jar_location" "${file##/tmp/alfresco-search-jar-source-override/}"; ' +
                        'done')
            // Leave the adjusted classes in the temp directory. It gives a clear overview of the changes.
        }
    }

    task integrationTestXenitEndpoints(type: Test, group: "verification") {
        enabled = (project.hasProperty('tests') && project.tests)
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        outputs.upToDateWhen { false }

        doFirst {
            dockerCompose.exposeAsSystemProperties(integrationTestXenitEndpoints)
            systemProperty("flavor", "${upstreamProject.solr.flavor}")
            systemProperty("telemetry", true)
            systemProperty("actuators", true)
            systemProperty("use_ssl", tests_use_ssl)
            systemProperty("keystore", "${rootProject.projectDir}/src/main/resources/global/keystore/ssl.repo.client.keystore")
            systemProperty("truststore", "${rootProject.projectDir}/src/main/resources/global/keystore/ssl.repo.client.truststore")

            if (project.name.contains('enterprise') && project.name.contains('-2')) {
                println("V2 logic applied to ${project.name}")
                include(project.property('defaultIntegrationTest'), project.property('v2IntegrationTest'))
            } else {
                include(project.property('defaultIntegrationTest'))
            }
        }
    }

    task integrationTests(type: Test, group: "verification") {
        dependsOn integrationTestXenitEndpoints
    }

    dockerCompose {
        environment.put 'ALFRESCO_IMAGE', upstreamProject.alfrescoimage
        environment.put 'INDEX', upstreamProject.solr.flavor

        isRequiredBy(project.tasks.integrationTestXenitEndpoints)

        useComposeFiles = composeFiles
    }

    task composeDownAll {
        dependsOn project.tasks.withType(ComposeDown)
    }
}
