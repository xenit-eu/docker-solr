plugins {
    id "eu.xenit.docker" version "5.3.0" apply false
    id "be.vbgn.ci-detect" version "0.5.0"
}

import java.util.stream.Collectors
allprojects {
    copyPropertyValueIfExists('eu_xenit_artifactory_username', 'eu.xenit.artifactory.username')
    copyPropertyValueIfExists('eu_xenit_artifactory_password', 'eu.xenit.artifactory.password')
}
def copyPropertyValueIfExists(sourcePropertyName, targetPropertyName) {
    if (project.hasProperty(sourcePropertyName)) {
        project.ext[targetPropertyName] = project.property(sourcePropertyName)
    }
}

def calcTags(version) {
    def tags = [
            "${version.major}.${version.minor}.${version.rev}".toString(),
            "${version.major}.${version.minor}".toString()
    ]

    if (version.maint) {
        tags += "${version.major}.${version.minor}.${version.rev}.${version.maint}".toString()
    }
    if (version.label) {
        tags += "${version.major}.${version.minor}.${version.rev}.${version.label}".toString()
        if (version.maint) {
            tags += "${version.major}.${version.minor}.${version.rev}.${version.maint}.${version.label}".toString()
        }
    }
    // For non-master/non-release builds, change the tags to contain branch and build number
    def isTestBuild = ci.isCi() && ci.branch != "master" && ci.branch != "release"
    if (isTestBuild) {
        tags = tags.stream().map({ it + "-build-${ci.branch}-${ci.buildNumber}" }).collect(Collectors.toList());
    }

    return tags
}

def calcRepository(flavor, enterprise, customized) {
    def repoName = (enterprise ? "private.docker.xenit.eu/alfresco-enterprise" : "docker.io/xenit")
    if (customized)
        return "${repoName}/alfresco-${flavor}-xenit"
    else
        return "${repoName}/alfresco-${flavor}"
}

subprojects {
    apply plugin: 'java'

    // use java 8 for tests to fix "javax.net.ssl.SSLHandshakeException: PKIX path validation failed: java.security.cert.CertPathValidatorException: Algorithm constraints check failed on signature algorithm: SHA1withRSA"
    plugins.withType(JavaBasePlugin) {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(8)
            }
        }
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/repositories/public/'
            metadataSources {
                artifact()
            }
            content {
                includeModule 'org.alfresco', 'alfresco-solr'
                includeModule 'org.alfresco', 'alfresco-solr4-distribution'
                includeModule 'org.alfresco', 'alfresco-search-services'
            }
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }


        // This private repository provides Xenit with Alfresco enterprise artefacts.
        // External developers should replace it with their own library repository.
        if (project.hasProperty('eu.xenit.artifactory.username') && project.hasProperty('eu.xenit.artifactory.password')) {
            maven {
                name 'Xenit artifactory release local'
                url 'https://artifactory.xenit.eu/artifactory/libs-release'
                credentials {
                    username property("eu.xenit.artifactory.username")
                    password property("eu.xenit.artifactory.password")
                }
            }
        }
    }

    pluginManager.withPlugin('eu.xenit.docker-config') {
        docker {
            if (System.getenv("DOCKER_USER") != null) {
                registryCredentials {
                    username = System.getenv("DOCKER_USER")
                    password = System.getenv("DOCKER_PASSWORD")
                }
            } else {
                logger.debug "using default credentials"
            }
        }
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDirs = ["${rootProject.projectDir}/src/integrationTest/java"]
            }
        }
    }

    configurations {
        runtime
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    dependencies {
        integrationTestCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.1'
        integrationTestCompile group: 'io.rest-assured', name: 'json-path', version: '3.0.1'
        integrationTestCompile group: 'io.rest-assured', name: 'rest-assured-common', version: '3.0.1'
        integrationTestCompile group: 'junit', name: 'junit', version: '4.11'

        integrationTestRuntime group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.2'
    }
}
